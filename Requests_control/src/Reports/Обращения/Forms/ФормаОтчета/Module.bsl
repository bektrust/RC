#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура КодВопросаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Раздел", Неопределено);
	ПараметрыФормы.Вставить("Тематика", Неопределено);
	ПараметрыФормы.Вставить("Тема", Неопределено);
	ПараметрыФормы.Вставить("Вопрос", Неопределено);
	ПараметрыФормы.Вставить("КодВопроса", КодВопроса);
	
	ПараметрыФормы.Вставить("РезультатРассмотрения", Неопределено);
	ПараметрыФормы.Вставить("ДатаОтвета", Неопределено);
	ПараметрыФормы.Вставить("ОрганДляПередачи", Неопределено);
	ПараметрыФормы.Вставить("ИсходящаяДатаДокумента", Неопределено);
	ПараметрыФормы.Вставить("ИсходящийНомерДокумента", Неопределено);
	ПараметрыФормы.Вставить("КлючСохраненияПоложенияОкна", "ВыборНесколькихВопросов");
	
	ПараметрыФормы.Вставить("МнениеАвтораОРезультатах", Неопределено);
	ПараметрыФормы.Вставить("МнениеАвтораОМерах", Неопределено);
	
	ПараметрыФормы.Вставить("СокращенныйРежим", Истина);
	
	Обработчик = Новый ОписаниеОповещения("КодыВопросовПослеВыбора", 
		ЭтотОбъект);
	ОткрытьФорму("Справочник.Обращения.Форма.ФормаВыбораИзКлассификатора", ПараметрыФормы,,,,,
		Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура КодыВопросовПослеВыбора(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда 
		КодВопроса = Результат.КодВопроса;
		КодВопросаПриИзменении(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВопросаПриИзменении(Элемент)

 	ПараметрКодВопроса = Новый ПараметрКомпоновкиДанных("КодВопроса"); 
	
	Для Каждого ЭлементНастроек Из Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
		
		Если (ТипЗнч(ЭлементНастроек) =  Тип("ЗначениеПараметраНастроекКомпоновкиДанных"))
		   И (ЭлементНастроек.Параметр = ПараметрКодВопроса) Тогда
		   
			ЭлементНастроек.Значение = КодВопроса;
			ЭлементНастроек.Использование = ЗначениеЗаполнено(КодВопроса);
			
		КонецЕсли;	
		
 	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойВариантаНаСервере(Настройки)
	
	Элементы.КодВопроса.Видимость = Истина;
	
	ПараметрКодВопроса = Новый ПараметрКомпоновкиДанных("КодВопроса"); 
	
	Для Каждого ЭлементНастроек Из Настройки.ПараметрыДанных.Элементы Цикл
		
		Если (ТипЗнч(ЭлементНастроек) =  Тип("ЗначениеПараметраНастроекКомпоновкиДанных"))
		   И (ЭлементНастроек.Параметр = ПараметрКодВопроса)
		   И ЭлементНастроек.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		   
			Элементы.КодВопроса.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти