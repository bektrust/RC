
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период.Вариант = ВариантСтандартногоПериода.ЭтотМесяц;
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	Результат.Очистить();
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетОтчета");
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.ОрганизацияНаименованиеПолное = Справочники.ОрганыИУчреждения.НашеУчреждение;
	Область.Параметры.ДатаНачала = Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy");
	Область.Параметры.ДатаОкончания = Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy");
	Область.Параметры.ДатаЗаполнения = Формат(ТекущаяДата, "ДФ=dd.MM.yyyy");
	
	Обращения = Справочники.Обращения.ОбращенияЗаПериод(Период.ДатаНачала,
		Период.ДатаОкончания);
	
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ТаблицаШапка");
	Результат.Вывести(Область);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА Обращения.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Обращения.ДатаПоступления
	|		КОГДА Обращения.ДатаПоОбращению <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Обращения.ДатаПоОбращению
	|		ИНАЧЕ Обращения.ДатаДокумента
	|	КОНЕЦ КАК ДатаСортировки,
	|	Обращения.Ссылка КАК ДокументСсылка,
	|	Обращения.ИсходящийНомер КАК ДокументНомер,
	|	ВЫБОР
	|		КОГДА Обращения.ИсходящийНомер = """"
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ Обращения.ДатаПоОбращению
	|	КОНЕЦ КАК ДокументДата,
	|	Обращения.ДатаПоступления КАК ПоступлениеДата,
	|	Обращения.ДатаДокумента КАК ПоступлениеДатаРегистрации,
	|	ОбращенияВопросы.ДатаОтвета КАК ОтветАвторуДата,
	|	Обращения.СрокРассмотренияПервоначальный КАК СрокРассмотренияПервоначальный,
	|	Обращения.СрокРассмотрения КАК СрокРассмотрения,
	|	ВЫБОР
	|		КОГДА Обращения.СрокРассмотрения > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокРассмотренияПервоначальный > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокРассмотрения > Обращения.СрокРассмотренияПервоначальный
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СрокРассмотренияПродлен,
	|	Обращения.Заявитель КАК Заявитель,
	|	Обращения.Заявитель.Наименование КАК ЗаявительНаименование,
	|	Обращения.Заявитель.АдресЭлектроннойПочты КАК ЗаявительАдресЭлектроннойПочты,
	|	Обращения.Заявитель.ПочтовыйАдрес КАК ЗаявительПочтовыйАдрес,
	|	ЕСТЬNULL(ОбращенияВопросы.НомерСтроки, 1) КАК НомерСтроки,
	|	ОбращенияВопросы.КодВопроса КАК ВопросКод,
	|	ОбращенияВопросы.РезультатРассмотрения КАК РезультатРассмотрения,
	|	ОбращенияВопросы.ОрганДляПередачи.Наименование КАК НаправленоГосУчреждение,
	|	ОбращенияВопросы.ИсходящаяДатаДокумента КАК НаправленоДата,
	|	ОбращенияВопросы.ИсходящийНомерДокумента КАК НаправленоНомер,
	|	ОрганыИУчреждения.Ссылка КАК НаправленоОрган,
	|	ОрганыИУчреждения.Представление КАК НаправленоОрганПредставление,
	|	ОрганыИУчреждения.ОрганУчреждение КАК НаправленоОрганТип,
	|	ОрганыИУчреждения.ИдентификаторССТУ КАК НаправленоОрганССТУ,
	|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОРезультатах, 1, 200) КАК МнениеАвтораОРезультате,
	|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОМерах, 1, 200) КАК МнениеАвтораОМерах
	|ПОМЕСТИТЬ ОбращенияИВопросы
	|ИЗ
	|	Справочник.Обращения КАК Обращения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Обращения.Вопросы КАК ОбращенияВопросы
	|		ПО Обращения.Ссылка = ОбращенияВопросы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОрганыИУчреждения КАК ОрганыИУчреждения
	|		ПО (ОбращенияВопросы.ОрганДляПередачи = ОрганыИУчреждения.Ссылка)
	|ГДЕ
	|	Обращения.Ссылка В(&Обращения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обращения.ДокументСсылка КАК ДокументСсылка,
	|	Обращения.ДокументНомер КАК ДокументНомер,
	|	Обращения.ДокументДата КАК ДокументДата,
	|	Обращения.ПоступлениеДата КАК ПоступлениеДата,
	|	Обращения.ПоступлениеДатаРегистрации КАК ПоступлениеДатаРегистрации,
	|	0 КАК ДокументНаправленоДата,
	|	0 КАК ДокументРезультатДанОтветАвтору,
	|	0 КАК ДокументРезультатОставленоБезОтвета,
	|	Обращения.РезультатРассмотрения КАК РезультатРассмотрения,
	|	ВЫБОР
	|		КОГДА Обращения.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК РезультатДанОтветАвтору,
	|	ВЫБОР
	|		КОГДА Обращения.СрокРассмотрения > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокРассмотренияПервоначальный > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокРассмотрения > Обращения.СрокРассмотренияПервоначальный
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СрокРассмотренияПродлен,
	|	Обращения.ОтветАвторуДата КАК РезультатДанОтветАвторуДата,
	|	Обращения.Заявитель КАК Заявитель,
	|	Обращения.ЗаявительНаименование КАК ЗаявительНаименование,
	|	Обращения.ЗаявительАдресЭлектроннойПочты КАК ЗаявительАдресЭлектроннойПочты,
	|	Обращения.ЗаявительПочтовыйАдрес КАК ЗаявительПочтовыйАдрес,
	|	Обращения.НомерСтроки КАК НомерСтроки,
	|	Обращения.ВопросКод КАК ВопросКод,
	|	Обращения.НаправленоДата КАК НаправленоДата,
	|	Обращения.НаправленоНомер КАК НаправленоНомер,
	|	ВЫБОР
	|		КОГДА Обращения.НаправленоОрганТип = ЗНАЧЕНИЕ(Перечисление.ОрганУчреждение.Учреждение)
	|			ТОГДА Обращения.НаправленоОрган
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаправленоГосУчреждение,
	|	ВЫБОР
	|		КОГДА Обращения.НаправленоОрганТип = ЗНАЧЕНИЕ(Перечисление.ОрганУчреждение.Учреждение)
	|			ТОГДА NULL
	|		ИНАЧЕ Обращения.НаправленоОрган
	|	КОНЕЦ КАК НаправленоИнойОгран,
	|	Обращения.НаправленоОрганПредставление КАК НаправленоОрганПредставление,
	|	Обращения.НаправленоОрганССТУ КАК НаправленоОрганССТУ,
	|	ВЫБОР
	|		КОГДА Обращения.МнениеАвтораОРезультате = """"
	|			ТОГДА NULL
	|		ИНАЧЕ Обращения.МнениеАвтораОРезультате
	|	КОНЕЦ КАК МнениеАвтораОРезультате,
	|	ВЫБОР
	|		КОГДА Обращения.МнениеАвтораОМерах = """"
	|			ТОГДА NULL
	|		ИНАЧЕ Обращения.МнениеАвтораОМерах
	|	КОНЕЦ КАК МнениеАвтораОМерах
	|ИЗ
	|	ОбращенияИВопросы КАК Обращения
	|
	|УПОРЯДОЧИТЬ ПО
	|	Обращения.ДатаСортировки,
	|	Обращения.ДокументНомер,
	|	Обращения.НомерСтроки
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.ДокументДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.ПоступлениеДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ПоступлениеДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.ПоступлениеДатаРегистрации > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ПоступлениеДатаРегистрации,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументНаправленоДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументРезультатДанОтветАвтору,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА Обращения.РезультатРассмотрения = ЗНАЧЕНИЕ(перечисление.РезультатыРассмотрения.ОставленоБезОтвета)
	|				ТОГДА Обращения.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументРезультатОставленоБезОтвета,
	|	КОЛИЧЕСТВО(РезультатДанОтветАвтору),
	|	КОЛИЧЕСТВО(ВопросКод),
	|	КОЛИЧЕСТВО(ВЫБОР
	|			КОГДА НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НаправленоДата
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК НаправленоДата,
	|	КОЛИЧЕСТВО(НаправленоГосУчреждение),
	|	КОЛИЧЕСТВО(НаправленоИнойОгран),
	|	КОЛИЧЕСТВО(МнениеАвтораОРезультате),
	|	КОЛИЧЕСТВО(МнениеАвтораОМерах)
	|ПО
	|	ОБЩИЕ,
	|	ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обращения.РезультатРассмотрения КАК РезультатРассмотрения,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	ОбращенияИВопросы КАК Обращения
	|ГДЕ
	|	Обращения.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.Поддержано)
	|	И Обращения.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Обращения.РезультатРассмотрения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Обращения.СрокРассмотренияПродлен,
	|	СУММА(1)
	|ИЗ
	|	ОбращенияИВопросы КАК Обращения
	|ГДЕ
	|	Обращения.РезультатРассмотрения = ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Обращения.СрокРассмотренияПродлен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.Поддержано),
	|	СУММА(1)
	|ИЗ
	|	ОбращенияИВопросы КАК Обращения
	|ГДЕ
	|	Обращения.РезультатРассмотрения В (ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.Поддержано), ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотрения.ВТомЧислеМерыПриняты))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Файлы.ВладелецФайла КАК ВладелецФайла,
	|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение КАК ПолноеИмя,
	|	Файлы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.ВладелецФайла В(&Обращения)
	|	И Файлы.ПометкаУдаления = ЛОЖЬ
	|	И Файлы.Ответный = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Файлы.ВладелецФайла,
	|	Файлы.Наименование");
	
	Запрос.УстановитьПараметр("Обращения", Обращения);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	//Кэш имен перечислений.
	ИменаРезультатов = Новый Соответствие;
	Для Каждого Элемент Из Метаданные.Перечисления.РезультатыРассмотрения.ЗначенияПеречисления Цикл
		Если Элемент.Имя <> "НаправленоВИнойОрган" Тогда
			ИменаРезультатов.Вставить(Перечисления.РезультатыРассмотрения[Элемент.Имя], Элемент.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ИменаРезультатов.Вставить(Истина,
		"НаходитсяНаРассмотренииСПродлением");
	ИменаРезультатов.Вставить(Ложь,
		"НаходитсяНаРассмотренииБезПродления");
	
	ФайлыОтветов = РезультатыЗапроса.Получить(3).Выгрузить();
	ФайлыОтветов.Индексы.Добавить("ВладелецФайла");
	ФайлыОтветовОтбор = Новый Структура("ВладелецФайла");
	
	ВыбИтог = РезультатыЗапроса.Получить(1).Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыбИтог.Следующий() Цикл
		
		ВыбДокумент = ВыбИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыбДокумент.Следующий() Цикл
			
			//Файлы ответов. На уровне документа в целом.
			Файлы = Новый Массив;
			ФайлыОтветовОтбор.ВладелецФайла = ВыбДокумент.ДокументСсылка;
			ФайлыСтроки = ФайлыОтветов.НайтиСтроки(ФайлыОтветовОтбор);
			Для Каждого Строка Из ФайлыСтроки Цикл
				Файлы.Добавить(Строка.ПолноеИмя);
			КонецЦикла;
			СтрФайлы = СтрСоединить(Файлы, ";" + Символы.ПС);
			
			КолВопросов = 0;
			ВыбВопрос = ВыбДокумент.Выбрать();
			Пока ВыбВопрос.Следующий() Цикл
				КолВопросов = КолВопросов + 1;
				
				Область = Макет.ПолучитьОбласть("ТаблицаСтрока");
				ЗаполнитьЗначенияСвойств(Область.Параметры, ВыбВопрос);
				
				Если ЗначениеЗаполнено(ВыбВопрос.НаправленоГосУчреждение) Тогда
					Область.Параметры.НаправленоГосУчреждениеПредставление = ВыбВопрос.НаправленоОрганПредставление;
				Иначе
					Область.Параметры.НаправленоИнойОгранПредставление = ВыбВопрос.НаправленоОрганПредставление;
				КонецЕсли;
				Если ПустаяСтрока(ВыбВопрос.ДокументНомер) Тогда
					Область.Параметры.ДокументНомер = "-";
				КонецЕсли;
				Если СтрФайлы <> "" Тогда
					Если ЗначениеЗаполнено(Область.Параметры.РезультатДанОтветАвторуДата) Тогда
						Область.Параметры.РезультатДанОтветАвторуДата = Формат(Область.Параметры.РезультатДанОтветАвторуДата, "ДФ=dd.MM.yyyy") +
							Символы.ПС +
							СтрФайлы;
					Иначе
						Область.Параметры.РезультатДанОтветАвторуДата = СтрФайлы;
					КонецЕсли;
					Область.Параметры.РезультатФайлОтвета = ФайлыСтроки.Получить(0).Ссылка;
				КонецЕсли;
				
				//Транспонирование результатов по колонкам.
				Если ЗначениеЗаполнено(ВыбВопрос.РезультатРассмотрения) Тогда 
					ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.РезультатРассмотрения);
				Иначе 
					ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.СрокРассмотренияПродлен);
				КонецЕсли;
				
				Если ИмяРезультата <> Неопределено Тогда
					Область.Параметры["Результат" + ИмяРезультата] = Истина;
					Если ИмяРезультата = "ВТомЧислеМерыПриняты" Тогда
						//Подчиненное значение.
						Область.Параметры.РезультатПоддержано = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если КолВопросов = 1 Тогда
					//Заполнение данных заявителя. Только в первой строке.
					Заявитель = "";
					Если ЗначениеЗаполнено(ВыбВопрос.ПоступлениеДатаРегистрации) Тогда
						Область.Параметры.ПоступлениеДатаРегистрации = Формат(ВыбВопрос.ПоступлениеДатаРегистрации, "ДФ=dd.MM.yyyy");
						Заявитель = Символы.ПС;
					КонецЕсли;
					Если ЗначениеЗаполнено(ВыбВопрос.Заявитель) Тогда
						Заявитель = Заявитель +
							"Автор: " + Строка(ВыбВопрос.ЗаявительНаименование);
						Если ЗначениеЗаполнено(ВыбВопрос.ЗаявительАдресЭлектроннойПочты)
							Или ЗначениеЗаполнено(ВыбВопрос.ЗаявительПочтовыйАдрес) Тогда
							Заявитель = Заявитель + "
								|Адрес для ответа:" +
								?(ЗначениеЗаполнено(ВыбВопрос.ЗаявительПочтовыйАдрес), "
								|почтовый: " + ВыбВопрос.ЗаявительПочтовыйАдрес, "") +
								?(ЗначениеЗаполнено(ВыбВопрос.ЗаявительАдресЭлектроннойПочты), "
								|e-mail: " + ВыбВопрос.ЗаявительАдресЭлектроннойПочты, "");
						КонецЕсли;
						Область.Параметры.ЗаявительПредставление = Заявитель;
					КонецЕсли;
				КонецЕсли;
				
				Результат.Вывести(Область);
			КонецЦикла; //Вопрос
			
			Если КолВопросов > 1 Тогда
				//Объединение строк в колонках по документу в целом.
				КолСтрок = Результат.ВысотаТаблицы;
				НомерНач = КолСтрок - КолВопросов + 1;
				Результат.Область(НомерНач, 1, КолСтрок, 2).Объединить();
				Для НК = 3 По 5 Цикл
					Результат.Область(НомерНач, НК, КолСтрок, НК).Объединить();
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла; //Документ
		
		// Итоги таблицы.
		Область = Макет.ПолучитьОбласть("ТаблицаИтог");
		ЗаполнитьЗначенияСвойств(Область.Параметры, ВыбИтог);
		
		ВыбВопрос = РезультатыЗапроса.Получить(2).Выбрать();
		Пока ВыбВопрос.Следующий() Цикл
			// Транспонирование результатов по колонкам.
			ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.РезультатРассмотрения);
			Если ИмяРезультата <> Неопределено Тогда
				Область.Параметры["Результат" + ИмяРезультата] = ВыбВопрос.Количество;
			КонецЕсли;
		КонецЦикла;
		
		Результат.Вывести(Область);
	
	КонецЦикла;
	
	Результат.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВССТУ(Команда)
	
	ПараметрыФормы = новый Структура;
	ПараметрыФормы.Вставить("ВыгружатьСразу", Истина);
	ПараметрыФормы.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыФормы.Вставить("ДатаОкончания", Период.ДатаОкончания);
	
	ОткрытьФорму("Обработка.ВыгрузкаВССТУ.Форма", ПараметрыФормы)
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Файл Excel (*.xls;*.xlsx)|*.xls;*.xlsx";
	ДиалогФайла.ПолноеИмяФайла = СтрШаблон(
		НСтр("ru = 'Регламентированный отчет за период с %1 по %2'"), 
		Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy"),
		Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy"));
		
	Если ДиалогФайла.Выбрать() Тогда
		
		ТипСохранения = ТипФайлаТабличногоДокумента.XLS;
		Файл = Новый Файл(ДиалогФайла.ПолноеИмяФайла);
		Если Найти(НРег(Файл.Расширение), "xlsx") Тогда
			ТипСохранения = ТипФайлаТабличногоДокумента.XLSX;
		Иначе	
			ТипСохранения = ТипФайлаТабличногоДокумента.XLS;
		КонецЕсли;	
		
		Результат.Записать(ДиалогФайла.ПолноеИмяФайла, ТипСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если Не ЗначениеЗаполнено(Расшифровка) Тогда 
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Файлы") Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Расшифровка,,УникальныйИдентификатор,,ПредыдущийАдресФайла);
		РаботаСФайламиКлиент.Открыть(ДанныеФайла, УникальныйИдентификатор);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти