////////////////////////////////////////////////////////////////////////////////
// Работа с HTML.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает ссылку.
//
Процедура ОткрытьСсылку(Href, Element) Экспорт
	
	ВыбраннаяСсылка = Неопределено;
	
	Если Href <> Неопределено Тогда
		// Если у данных события заполнено свойство Href - будем считать что переход будет по этой ссылке.
		ВыбраннаяСсылка = Href;
	Иначе
		Попытка
			// Если у элемента события заполнено свойство Href и элемент AREA - будем считать что переход будет по этой ссылке.
			Если ВРег(Element.tagName) = "AREA" Тогда
				ВыбраннаяСсылка = Element.Href;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если ПустаяСтрока(ВыбраннаяСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
	Если Найти(ВыбраннаяСсылка, "javascript:_1c") = 1 Тогда
		
		// FF - используется javascript такого вида
		ПозицияНавигационнойСсылки = Найти(ВыбраннаяСсылка, "e1cib/");
		
		Если ПозицияНавигационнойСсылки <> 0 Тогда
			ВыбраннаяСсылка = 
				"v8doc:" 
				+ Сред(
					ВыбраннаяСсылка, 
					ПозицияНавигационнойСсылки, 
					СтрДлина(ВыбраннаяСсылка) - ПозицияНавигационнойСсылки - 2);
		Иначе
			СтандартнаяОбработка = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ПозицияРазделителя = Найти(ВыбраннаяСсылка, "#");
	ПозицияВнутреннейНавигационнойСсылки = Найти(ВыбраннаяСсылка, "#e1cib/");
	
	Если ПозицияРазделителя = 1  Тогда
		
		// Safari - передается просто относительная ссылка
		СтандартнаяОбработка = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если Найти(ВыбраннаяСсылка, ПолучитьНавигационнуюСсылкуИнформационнойБазы()) = 1 Тогда
		
		// Chrome, IE - передаётся полная ссылка
		Если ПозицияРазделителя <> 0 И ПозицияВнутреннейНавигационнойСсылки = 0 Тогда
			
			СтандартнаяОбработка = Истина;
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецЕсли
	
	СхемаСсылки = ОпределитьСхемуСсылки(ВыбраннаяСсылка);
	
	Если СхемаСсылки = "v8doc:" Тогда
		
		ОбработатьСсылку1СДокументооборота(ВыбраннаяСсылка);
		
	ИначеЕсли СхемаСсылки = "e1c://" Тогда
		
		ПерейтиПоНавигационнойСсылке(ВыбраннаяСсылка);
		
	ИначеЕсли (СхемаСсылки = "http://" И Найти(ВыбраннаяСсылка, "e1cib") > 0)
		ИЛИ (СхемаСсылки = "https://" И Найти(ВыбраннаяСсылка, "e1cib") > 0) Тогда
		
		Если Не ПерейтиПоВнутреннейНавигационнойСсылке(ВыбраннаяСсылка) Тогда
			ПерейтиПоНавигационнойСсылке(ВыбраннаяСсылка);
		КонецЕсли;
		
	ИначеЕсли СхемаСсылки = "mailto:"
		И ПараметрыОтправкиПочтовогоСообщенияПовтИсп.ЕстьДоступныеПрофилиПочты() Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("СсылкаMailto", Href);
		ПараметрыОткрытия.Вставить("Объекты", Новый Массив);
		
		ОткрытьФорму(
			"Обработка.ПочтовоеСообщение.Форма.Форма",
			ПараметрыОткрытия,,
			Новый УникальныйИдентификатор);
		
	ИначеЕсли СхемаСсылки = "http://"
		Или СхемаСсылки = "https://"
		Или СхемаСсылки = "ftp://"
		Или СхемаСсылки = "file://"
		Или СхемаСсылки = "mailto:" Тогда
		
		#Если ВебКлиент Тогда
		ПерейтиПоНавигационнойСсылке(ВыбраннаяСсылка);
		#Иначе
		ЗапуститьПриложение(ВыбраннаяСсылка);
		#КонецЕсли
		
	Иначе
		
		ПерейтиПоНавигационнойСсылке(ВыбраннаяСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обрабатывает ссылку 1С:Документооборота
Процедура ОбработатьСсылку1СДокументооборота(Ссылка1СДокументооборота)
	
	СхемаСсылок1СДокументооборота = "v8doc:";
	
	Если  ОпределитьСхемуСсылки(Ссылка1СДокументооборота) <> СхемаСсылок1СДокументооборота Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка1СДокументооборота = Сред(Ссылка1СДокументооборота, СтрДлина(СхемаСсылок1СДокументооборота) + 1);
	
	Если СтрНачинаетсяС(НРег(Ссылка1СДокументооборота), "e1cib/") Тогда
		
		ПерейтиПоНавигационнойСсылке(Ссылка1СДокументооборота);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет попытку перехода по внутренней навигационной ссылке
//
Функция ПерейтиПоВнутреннейНавигационнойСсылке(НавигационнаяСсылка)
	
	ПозицияВнутреннейНавигационнойСсылки = Найти(НавигационнаяСсылка, "#e1cib/");
	Если ПозицияВнутреннейНавигационнойСсылки = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВнутренняяНавигационнаяСсылка = Сред(НавигационнаяСсылка, ПозицияВнутреннейНавигационнойСсылки + 1);
	Попытка
		ПерейтиПоНавигационнойСсылке(ВнутренняяНавигационнаяСсылка);
	Исключение
		// Внутренней навигационный ссылки может не быть в базе.
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Определяет схему ссылки
Функция ОпределитьСхемуСсылки(Href)
	
	НРегHref = НРег(Href);
	
	Если СтрНачинаетсяС(НРегHref, "v8doc:") Тогда
		
		Возврат "v8doc:";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "http://") Тогда
		
		Возврат "http://";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "https://") Тогда
		
		Возврат "https://";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "ftp://") Тогда
		
		Возврат "ftp://";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "e1c://") Тогда
		
		Возврат "e1c://";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "file://") Тогда
		
		Возврат "file://";
		
	ИначеЕсли СтрНачинаетсяС(НРегHref, "mailto:") Тогда
		
		Возврат "mailto:";
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти