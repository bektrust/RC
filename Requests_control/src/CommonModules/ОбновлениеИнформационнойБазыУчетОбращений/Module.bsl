////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы демонстрационной конфигурации (БиблиотекаСтандартныхПодсистемДемо).
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Сведения о библиотеке (или конфигурации).

// См. описание в общем модуле ОбновлениеИнформационнойБазыБСП.
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя = Метаданные.Имя;
	Описание.Версия = Метаданные.Версия;
	
	// Требуется библиотека стандартных подсистем.
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления информационной базы.

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре.
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.РежимВыполнения     = "Монопольно";
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	// Обработчики, выполняемые при каждом обновлении ИБ.
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.УправлениеОбработчиками = Истина;
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ВыполнятьВсегдаПриСменеВерсии";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ОбработчикСОшибкой";
	Обработчик.РежимВыполнения = "Оперативно";
	
	// Варианты отчетов ДО
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.Процедура = "НастройкиВариантовОтчетовДокументооборот.ЗаполнитьНастройкиВариантовОтчетовДокументооборот";
	Обработчик.РежимВыполнения = "Оперативно";
	
	// Обработчики, выполняемые при заполнении пустой ИБ.
	//
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ПервыйЗапуск";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ОбновитьПредопределенныеКлючевыеОперации";
	
	// Обработчики, выполняемые при переходе на новую версию.
	//
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "1.0.1.1";
	Обработчик.Процедура = "ВыгрузкаВССТУ.НастроитьУзлы";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "1.0.3.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ПерейтиНаВерсию_1_0_3_1";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "1.0.4.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ПерейтиНаВерсию_1_0_4_1";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия    = "1.0.5.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ПерейтиНаВерсию_1_0_5_1";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.5.2";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Процедура = 
		"ОбновлениеИнформационнойБазыУчетОбращений.ЗаполнениеПервоначальногоСрокаРассмотрения_1_0_5_2";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("5df396fe-b3eb-42f0-b3ed-34eab9f6a144");
	Обработчик.Комментарий = НСтр(
		"ru = 'Заполнение реквизита ""Срок рассмотрения первоначальный"" справочника ""Обращения"".'");
	
КонецПроцедуры

// Вызывается перед процедурами-обработчиками обновления данных ИБ.
//
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
КонецПроцедуры

// Вызывается после завершения обновления данных ИБ.
// 
// Параметры:
//   ПредыдущаяВерсия       - Строка - версия до обновления. "0.0.0.0" для "пустой" ИБ.
//   ТекущаяВерсия          - Строка - версия после обновления.
//   ВыполненныеОбработчики - ДеревоЗначений - список выполненных процедур-обработчиков обновления,
//                                             сгруппированных по номеру версии.
//   ВыводитьОписаниеОбновлений - Булево - если установить Истина, то будет выведена форма
//                                с описанием обновлений. По умолчанию, Истина.
//                                Возвращаемое значение.
//   МонопольныйРежим           - Булево - Истина, если обновление выполнялось в монопольном режиме.
//
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
		Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	
КонецПроцедуры

// Вызывается при подготовке табличного документа с описанием изменений в программе.
//
// Параметры:
//   Макет - ТабличныйДокумент - описание обновления всех библиотек и конфигурации.
//           Макет можно дополнить или заменить.
//           См. также общий макет ОписаниеИзмененийСистемы.
//
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	
КонецПроцедуры

// Позволяет переопределить режим обновления данных информационной базы.
// Для использования в редких (нештатных) случаях перехода, не предусмотренных в
// стандартной процедуре определения режима обновления.
//
// Параметры:
//   РежимОбновленияДанных - Строка - в обработчике можно присвоить одно из значений:
//              "НачальноеЗаполнение"     - если это первый запуск пустой базы (области данных);
//              "ОбновлениеВерсии"        - если выполняется первый запуск после обновление конфигурации базы данных;
//              "ПереходСДругойПрограммы" - если выполняется первый запуск после обновление конфигурации базы данных, 
//                                          в которой изменилось имя основной конфигурации.
//
//   СтандартнаяОбработка  - Булево - если присвоить Ложь, то стандартная процедура
//                                    определения режима обновления не выполняется, 
//                                    а используется значение РежимОбновленияДанных.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Добавляет в список процедуры-обработчики перехода с другой программы (с другим именем конфигурации).
// Например, для перехода между разными, но родственными конфигурациями: базовая -> проф -> корп.
// Вызывается перед началом обновления данных ИБ.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - с колонками:
//    * ПредыдущееИмяКонфигурации - Строка - имя конфигурации, с которой выполняется переход;
//                                           или "*", если нужно выполнять при переходе с любой конфигурации.
//    * Процедура                 - Строка - полное имя процедуры-обработчика перехода с программы
//                                           ПредыдущееИмяКонфигурации.
//                                  Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику"
//                                  Обязательно должна быть экспортной.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.ПредыдущееИмяКонфигурации  = "УправлениеТорговлей";
//  Обработчик.Процедура                  = "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику";
//
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.ПредыдущееИмяКонфигурации = "УчетОбращенийБазовая";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыУчетОбращений.ПерейтиСБазовойВерсииНаПРОФ";
	
КонецПроцедуры

// Вызывается после выполнения всех процедур-обработчиков перехода с другой программы (с другим именем конфигурации),
// и до начала выполнения обновления данных ИБ.
//
// Параметры:
//  ПредыдущееИмяКонфигурации    - Строка - имя конфигурации до перехода.
//  ПредыдущаяВерсияКонфигурации - Строка - имя предыдущей конфигурации (до перехода).
//  Параметры                    - Структура - 
//    * ВыполнитьОбновлениеСВерсии   - Булево - по умолчанию Истина. Если установить Ложь, 
//        то будут выполнена только обязательные обработчики обновления (с версией "*").
//    * ВерсияКонфигурации           - Строка - номер версии после перехода. 
//        По умолчанию, равен значению версии конфигурации в свойствах метаданных.
//        Для того чтобы выполнить, например, все обработчики обновления с версии ПредыдущаяВерсияКонфигурации, 
//        следует установить значение параметра в ПредыдущаяВерсияКонфигурации.
//        Для того чтобы выполнить вообще все обработчики обновления, установить значение "0.0.0.1".
//    * ОчиститьСведенияОПредыдущейКонфигурации - Булево - по умолчанию Истина. 
//        Для случаев когда предыдущая конфигурация совпадает по имени с подсистемой текущей конфигурации, следует
//        указать Ложь.
//
Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации, 
	Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
	
	Если ПредыдущееИмяКонфигурации = "ПредыдущееИмяКонфигурацииБазовая" Тогда
		Параметры.ВерсияКонфигурации = ПредыдущаяВерсияКонфигурации;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение пустой ИБ

// Демонстрирует процедуру-обработчик обновления и первоначального заполнения
// данных ИБ, которая выполняется однократно при переходе на версию 1.0.0.0.
//
Процедура ПервыйЗапуск() Экспорт
	
	Константы.ИспользоватьДополнительныеРеквизитыИСведения.Установить(Ложь);
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = Истина;
	
	Константы.ВыполнятьЗамерыПроизводительности.Установить(Истина);
	
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда 
		Константы.ПоказыватьПользователей.Установить(Ложь);
	Иначе 
		Константы.ПоказыватьПользователей.Установить(Истина);
	КонецЕсли;
	
	// Настройка центра мониторинга "по умолчанию"
	ПараметрыЦентраМониторинга = ЦентрМониторингаСлужебный.ПолучитьПараметрыЦентраМониторингаВнешнийВызов();
	
	ПараметрыСервисаПоУмолчанию = ЦентрМониторингаСлужебный.ПолучитьПараметрыПоУмолчаниюВнешнийВызов();
	ПараметрыЦентраМониторинга.Вставить("СерверПоУмолчанию", ПараметрыСервисаПоУмолчанию.Сервер);
	ПараметрыЦентраМониторинга.Вставить("АдресРесурсаПоУмолчанию", ПараметрыСервисаПоУмолчанию.АдресРесурса);
	ПараметрыЦентраМониторинга.Вставить("ПортПоУмолчанию", ПараметрыСервисаПоУмолчанию.Порт);
	ПараметрыЦентраМониторинга.Вставить("ВключитьЦентрМониторинга", Истина);
	ПараметрыЦентраМониторинга.Вставить("РегистрироватьПроизводительность", Истина);
	ПараметрыЦентраМониторинга.Вставить("РегистрироватьПроизводительностьТехнологическую", Истина);
	
	ЦентрМониторингаСлужебный.УстановитьПараметрыЦентраМониторингаВнешнийВызов(ПараметрыЦентраМониторинга);
	
	РегЗадание = ЦентрМониторингаСлужебный.ПолучитьРегламентноеЗаданиеВнешнийВызов("СборИОтправкаСтатистики", Истина);
	ЦентрМониторингаСлужебный.УстановитьРасписаниеПоУмолчаниюВнешнийВызов(РегЗадание);
	// Конец настройки центра мониторинга
	
	// Виды обращений
	ВидыОбращений = Новый СписокЗначений;
	ВидыОбращений.Добавить("Жалоба");
	ВидыОбращений.Добавить("Запрос");
	ВидыОбращений.Добавить("Заявление");
	ВидыОбращений.Добавить("Устное обращение");
	
	Для Каждого ВидОбращения Из ВидыОбращений Цикл
		Если Справочники.ВидыОбращений.НайтиПоНаименованию(ВидОбращения.Значение).Пустая() Тогда
			ВидОбращенияОбъект = Справочники.ВидыОбращений.СоздатьЭлемент();
			ВидОбращенияОбъект.Наименование = ВидОбращения.Значение;
			ВидОбращенияОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
	// Способы доставки
	СпособыДоставки = Новый СписокЗначений;
	СпособыДоставки.Добавить("Почта");
	СпособыДоставки.Добавить("Email");
	СпособыДоставки.Добавить("Факс");
	СпособыДоставки.Добавить("Курьер");
	СпособыДоставки.Добавить("Фельдсвязь");
	
	Для Каждого СпособДоставки Из СпособыДоставки Цикл
		Если Справочники.СпособыДоставки.НайтиПоНаименованию(СпособДоставки.Значение).Пустая() Тогда
			СпособДоставкиОбъект = Справочники.СпособыДоставки.СоздатьЭлемент();
			СпособДоставкиОбъект.Наименование = СпособДоставки.Значение;
			СпособДоставкиОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
	// нумератор
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Нумераторы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Нумераторы КАК Нумераторы
	               |ГДЕ
	               |	Нумераторы.ПометкаУдаления = ЛОЖЬ";
	Если Не Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;	
	
	НумераторОбъект = Справочники.Нумераторы.СоздатьЭлемент();
	НумераторОбъект.Наименование = НСтр("ru = 'По умолчанию'");
	НумераторОбъект.ФорматНомера  = "[Номер]";
	Нумерация.СформироватьПримерНомера(НумераторОбъект.ФорматНомера, НумераторОбъект.Пример, ""); 
	НумераторОбъект.Записать();
	
	МенеджерЗаписи = РегистрыСведений.НастройкиНумерации.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВидОбращения = Справочники.ВидыОбращений.ПустаяСсылка();
	МенеджерЗаписи.СпособНумерации = Перечисления.СпособыНумерации.Автоматически;
	МенеджерЗаписи.Нумератор = НумераторОбъект.Ссылка;
	
	МенеджерЗаписи.Записать();
	
	// Показывать пользователей
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда 
		Константы.ПоказыватьПользователей.Установить(Ложь);
	Иначе 
		Константы.ПоказыватьПользователей.Установить(Истина);
	КонецЕсли;
	
	Константы.ДатаРедакцииКлассификатора.Установить(ТекущаяДатаКлассификатора());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление ИБ

// Демонстрирует процедуру-обработчик обновления данных ИБ, 
// которая выполняется каждый раз при смене версии конфигурации.
//
Процедура ВыполнятьВсегдаПриСменеВерсии(Параметры = Неопределено) Экспорт
	
	// Проверка необходимости обновления критичных общих данных.
	ТребуетсяОбновитьКритичныеОбщиеДанные = Ложь;
	// Конец Проверка необходимости обновления критичных общих данных.
	Если ТребуетсяОбновитьКритичныеОбщиеДанные Тогда
		Если Параметры <> Неопределено И НЕ Параметры.МонопольныйРежим Тогда
			Параметры.МонопольныйРежим = Истина;
			Возврат;
		КонецЕсли;
		// Код обновления критичных общих данных.
	КонецЕсли;
	
КонецПроцедуры

// Тестовый обработчик, предназначенный для имитации ошибки при обновлении.
//
Процедура ОбработчикСОшибкой() Экспорт
	
	ИмитироватьОшибку = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ОбновлениеИБ", "ИмитироватьОшибкуПриОбновлении", , ИмяПользователя());
	Если ИмитироватьОшибку = Истина Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбновлениеИБ",
			"ИмитироватьОшибкуПриОбновлении", Ложь, ИмяПользователя());
		ТекстСообщения = НСтр("ru = 'Процедура ОбработчикСОшибкой выполнилась с ошибкой.'");
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			ТекстСообщения);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

// Обновляет значения реквизитов предопределенных ключевых операций.
Процедура ОбновитьПредопределенныеКлючевыеОперации() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КлючевыеОперации.Ссылка,
	               |	КлючевыеОперации.ИмяПредопределенныхДанных
	               |ИЗ
	               |	Справочник.КлючевыеОперации КАК КлючевыеОперации
	               |ГДЕ
	               |	КлючевыеОперации.Имя = """"
	               |	И КлючевыеОперации.Предопределенный";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		КлючеваяОперация = Выборка.Ссылка.ПолучитьОбъект();
		КлючеваяОперация.Имя = Выборка.ИмяПредопределенныхДанных;
		КлючеваяОперация.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Пример перехода с конфигурации с именем "БиблиотекаСтандартныхПодсистемДемоБазовая".
// 
Процедура ПерейтиСБазовойВерсииНаПРОФ() Экспорт
	
	Константы.ПоказыватьПользователей.Установить(Истина);
	
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,,
		НСтр("ru = 'Выполнен обработчик перехода ПерейтиСБазовойВерсииНаПРОФ'", Метаданные.ОсновнойЯзык.КодЯзыка));
	
КонецПроцедуры

// Выполняется на версию 2.3.1.21. Актуализирует признак использования
// наборов свойств справочника Внешние пользователи.
//
Процедура ОбновитьИспользованиеНаборовСвойствВнешнихПользователей() Экспорт
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьВнешнихПользователей");
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Справочник_ВнешниеПользователи", ПараметрыНабора);
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_1_0_3_1() Экспорт 
	
	ПометитьНаУдалениеСтарыеВопросы(Дата(2015, 02, 12), Дата(1, 1, 1));
	Константы.АктуализироватьКлассификатор.Установить(Истина);
	Константы.ДатаРедакцииКлассификатора.Установить(Дата(2017, 11, 30));
	ЗаписьЖурналаРегистрации("Запись константы Обновление версии", УровеньЖурналаРегистрации.Предупреждение,,, "Попали в процедуру 4" 
		+ СокрЛП(Константы.АктуализироватьКлассификатор.Получить()));
	
КонецПроцедуры

Процедура ПометитьНаУдалениеСтарыеВопросы(НоваяРедакция, СтараяРедакция )
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Разделы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Разделы КАК Разделы
		|ГДЕ
		|	НЕ Разделы.ПометкаУдаления
		|	И Разделы.РедакцияКлассификатора = &Редакция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Тематики.Ссылка
		|ИЗ
		|	Справочник.Тематики КАК Тематики
		|ГДЕ
		|	НЕ Тематики.ПометкаУдаления
		|	И Тематики.РедакцияКлассификатора = &Редакция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Темы.Ссылка
		|ИЗ
		|	Справочник.Темы КАК Темы
		|ГДЕ
		|	НЕ Темы.ПометкаУдаления
		|	И Темы.РедакцияКлассификатора = &Редакция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Вопросы.Ссылка
		|ИЗ
		|	Справочник.Вопросы КАК Вопросы
		|ГДЕ
		|	НЕ Вопросы.ПометкаУдаления
		|	И Вопросы.РедакцияКлассификатора = &Редакция";
	
	Запрос.Параметры.Вставить("Редакция", СтараяРедакция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		ОбъектСсылка = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ЗначениеЗаполнено(НоваяРедакция) И ОбъектСсылка.РедакцияКлассификатора <> НоваяРедакция Тогда 
			ОбъектСсылка.РедакцияКлассификатора = НоваяРедакция;
			ОбъектСсылка.Записать();
		КонецЕсли;
		
		ОбъектСсылка.УстановитьПометкуУдаления(Истина);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_1_0_4_1() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.НайтиПоКоду(1);
	Если НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.ПустаяСсылка() Тогда 
		НовыйЭлемент = Справочники.СрокиРассмотренияОбращений.СоздатьЭлемент();
	Иначе 
		НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЭлемент.Код = 1;
	НовыйЭлемент.Наименование = НСтр("ru = '1 день'");
	НовыйЭлемент.Записать();
	НовыйЭлемент.УстановитьПометкуУдаления(Ложь);
	
	НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.НайтиПоКоду(5);
	Если НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.ПустаяСсылка() Тогда 
		НовыйЭлемент = Справочники.СрокиРассмотренияОбращений.СоздатьЭлемент();
	Иначе 
		НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЭлемент.Код = 5;
	НовыйЭлемент.Наименование = НСтр("ru = '5 дней'");
	НовыйЭлемент.Записать();
	НовыйЭлемент.УстановитьПометкуУдаления(Ложь);
	
	НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.НайтиПоКоду(7);
	Если НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.ПустаяСсылка() Тогда 
		НовыйЭлемент = Справочники.СрокиРассмотренияОбращений.СоздатьЭлемент();
	Иначе 
		НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЭлемент.Код = 7;
	НовыйЭлемент.Наименование = НСтр("ru = '7 дней'");
	НовыйЭлемент.Записать();
	НовыйЭлемент.УстановитьПометкуУдаления(Ложь);
	
	НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.НайтиПоКоду(15);
	Если НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.ПустаяСсылка() Тогда 
		НовыйЭлемент = Справочники.СрокиРассмотренияОбращений.СоздатьЭлемент();
	Иначе 
		НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЭлемент.Код = 15;
	НовыйЭлемент.Наименование = НСтр("ru = '15 дней'");
	НовыйЭлемент.Записать();
	НовыйЭлемент.УстановитьПометкуУдаления(Ложь);
	
	НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.НайтиПоКоду(30);
	Если НайденнаяСсылка = Справочники.СрокиРассмотренияОбращений.ПустаяСсылка() Тогда 
		НовыйЭлемент = Справочники.СрокиРассмотренияОбращений.СоздатьЭлемент();
	Иначе 
		НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйЭлемент.Код = 30;
	НовыйЭлемент.Наименование = НСтр("ru = '30 дней'");
	НовыйЭлемент.Записать();
	НовыйЭлемент.УстановитьПометкуУдаления(Ложь);
	
КонецПроцедуры

Процедура ПерейтиНаВерсию_1_0_5_1() Экспорт 
	
	ТекущаяАкутальнаяДата = Константы.ДатаРедакцииКлассификатора.Получить();
	ПометитьНаУдалениеСтарыеВопросы(ТекущаяАкутальнаяДата, ТекущаяАкутальнаяДата);
	Константы.АктуализироватьКлассификатор.Установить(Истина);
	Константы.ДатаРедакцииКлассификатора.Установить(ТекущаяДатаКлассификатора());
	
КонецПроцедуры

Процедура ЗаполнениеПервоначальногоСрокаРассмотрения_1_0_5_2(Параметры) Экспорт
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		ЗапросКоличествоОбращений = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(Обращения.Ссылка) КАК Количество
			|ИЗ
			|	Справочник.Обращения КАК Обращения
			|ГДЕ
			|	Обращения.СрокРассмотрения <> ДАТАВРЕМЯ(1, 1, 1)
			|	И Обращения.СрокРассмотренияПервоначальный = ДАТАВРЕМЯ(1, 1, 1)");
		
		РезультатЗапроса = ЗапросКоличествоОбращений.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = ?(Выборка.Следующий(), Выборка.Количество, 0);
		
	КонецЕсли;
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	// Заполнение реквизита "СрокРассмотренияПервоначальный" справочника "Обращения".
	ЗапросОбращения = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Обращения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Обращения КАК Обращения
		|ГДЕ
		|	Обращения.СрокРассмотрения <> ДАТАВРЕМЯ(1, 1, 1)
		|	И Обращения.СрокРассмотренияПервоначальный = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Обращения.ДатаДокумента УБЫВ");
	
	РезультатЗапросаОбращений = ЗапросОбращения.Выполнить();
	ВыборкаОбращений = РезультатЗапросаОбращений.Выбрать();
	Пока ВыборкаОбращений.Следующий() Цикл
		
		Попытка
			
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.Обращения");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаОбращений.Ссылка);
				Блокировка.Заблокировать();
				
				ОбращениеОбъект = ВыборкаОбращений.Ссылка.ПолучитьОбъект();
				Если Не ЗначениеЗаполнено(ОбращениеОбъект.СрокРассмотренияПервоначальный) Тогда
					ОбращениеОбъект.СрокРассмотренияПервоначальный = ОбращениеОбъект.СрокРассмотрения;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбращениеОбъект);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			
		Исключение
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать Обращение %1 по причине:
					|%2'"),
				ВыборкаОбращений.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.Обращения,
				ВыборкаОбращений.Ссылка,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ЗаполнениеПервоначальногоСрокаРассмотрения_1_0_5_2 не удалось обработать некоторые Обращения (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	Параметры.ОбработкаЗавершена = ОбработаноОбъектов = 0;
	
КонецПроцедуры

Функция ТекущаяДатаКлассификатора()
	
	Возврат Дата(2018, 09, 20);
	
КонецФункции

#КонецОбласти