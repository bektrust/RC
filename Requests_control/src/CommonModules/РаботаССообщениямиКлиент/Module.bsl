////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с сообщениями.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет регистрацию схемы e1c.
//
Процедура ЗарегистрироватьСхемуE1c() Экспорт
	
	#Если Не ВебКлиент Тогда
		
		КаталогПрограммы = КаталогПрограммы();
		Если Не ЗначениеЗаполнено(КаталогПрограммы) Тогда
			Возврат;
		КонецЕсли;
		
		Если Не СтрЗаканчиваетсяНа(КаталогПрограммы, "\") Тогда
			КаталогПрограммы = КаталогПрограммы + "\";
		КонецЕсли;
		
		КоманднаяСтрокаСхемыE1c = Новый Массив;
		КоманднаяСтрокаСхемыE1c.Добавить("""");
		КоманднаяСтрокаСхемыE1c.Добавить(КаталогПрограммы);
		КоманднаяСтрокаСхемыE1c.Добавить("1cv8c.exe"" /url ""%1""");
		КоманднаяСтрокаСхемыE1c = СтрСоединить(КоманднаяСтрокаСхемыE1c);
		КоманднаяСтрокаСхемыE1c = СтрЗаменить(КоманднаяСтрокаСхемыE1c, "\", "\\");
		КоманднаяСтрокаСхемыE1c = СтрЗаменить(КоманднаяСтрокаСхемыE1c, """", "\""");
		
		ТекстRegedit = СтрШаблон(
			"REGEDIT4
			|
			|[HKEY_CLASSES_ROOT\e1c]
			|@=""URL:e1c Protocol""
			|""URL Protocol""=""""
			|
			|[HKEY_CLASSES_ROOT\e1c\shell]
			|
			|[HKEY_CLASSES_ROOT\e1c\shell\open]
			|
			|[HKEY_CLASSES_ROOT\e1c\shell\open\command]
			|@=""%1""",
			КоманднаяСтрокаСхемыE1c);
		
		ИмяREGФайла = ПолучитьИмяВременногоФайла("reg");
		ЗаписьТекста = Новый ЗаписьТекста(ИмяREGФайла, "UTF-16");
		ЗаписьТекста.Записать(ТекстRegedit);
		ЗаписьТекста.Закрыть();
		
		Попытка
			Оболочка = Новый COMОбъект("WScript.Shell");
			Команда = СтрШаблон("regedit.exe /s ""%1""", ИмяREGФайла);
			Оболочка.Run(Команда, 0, Ложь);
		Исключение
			// Ошибка при попытке регистрации. Больше не пробуем.
		КонецПопытки;
		
		РаботаССообщениямиВызовСервера.ОбновитьИнформациюОКаталогеПрограммы();
		
	#КонецЕсли
	
КонецПроцедуры

// Выполняет обновление использования создания сообщений.
Процедура ОбновитьИспользованиеСозданияСообщений() Экспорт
	
	ИзмененоЗначениеИспользования = РаботаССообщениямиВызовСервера.ОбновитьИспользованиеСозданияСообщений();
	Если ИзмененоЗначениеИспользования Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти