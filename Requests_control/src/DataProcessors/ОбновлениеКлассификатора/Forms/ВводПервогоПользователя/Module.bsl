
&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьПервогоПользователя();	
	
	ПараметрыЗапуска = "/N" + Логин;
	Если Не ПустаяСтрока(Пароль) Тогда
		ПараметрыЗапуска = ПараметрыЗапуска + " /P" + Пароль;
	КонецЕсли;	
	ЗавершитьРаботуСистемы(Ложь, Истина, ПараметрыЗапуска);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПервогоПользователя()
	
	Пользователь = Справочники.Пользователи.СоздатьЭлемент();
	
	Админ = ПользователиИнформационнойБазы.СоздатьПользователя();
	
	Админ.АутентификацияСтандартная = Истина;
	Админ.Имя = Логин;
	Админ.ПолноеИмя = ФИО;
	Админ.РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение;
	Админ.Роли.Добавить(Метаданные.Роли.АдминистраторСистемы);
	Админ.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
	Админ.Роли.Добавить(Метаданные.Роли.Пользователь);
	Админ.Пароль = Пароль;
	
	Админ.Записать();
	
	Пользователь.Наименование = ФИО;
	Пользователь.АдресЭлектроннойПочты = Email;
	Пользователь.Должность = Должность;
	Пользователь.ПредставлениеВДокументах = ВДокументах;
	Пользователь.ИдентификаторПользователяИБ = Админ.УникальныйИдентификатор;
	Пользователь.Записать();
	
КонецПроцедуры		

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Элементы.ДекорацияЗаголовокПроф.Видимость = Ложь;
		Элементы.ДекорацияПользователиПотомПроф.Видимость = Ложь;
	Иначе	
		Элементы.ДекорацияЗаголовокБазовая.Видимость = Ложь;
		Элементы.ДекорацияПользователиПотомБазовая.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФИОПриИзменении(Элемент)
	
	Логин = ПользователиСлужебныйКлиентСервер.ПолучитьКраткоеИмяПользователяИБ(ФИО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершитьРаботуСистемы();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Email)
		И Не РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(Email) Тогда
		
		ТекстОшибки = НСтр("ru = 'Укажите корректный адрес электронной почты'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,
				"Email",, Отказ);
			
	КонецЕсли;
	
КонецПроцедуры
