
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИзменениеПароля = Ложь;
	Элементы.ИзменитьПароль.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.Недействителен.Доступность = Ложь;
	КонецЕсли;	
	
	Если Не Объект.Недействителен Тогда
		
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			Объект.ИдентификаторПользователяИБ);
			
		Если ПользовательИБ <> Неопределено Тогда
				
			Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава) Тогда
				ПолныеПрава = Истина;
			КонецЕсли;	
			
			Логин = ПользовательИБ.Имя;
			
			Если ПользовательИБ.ПарольУстановлен Тогда
				Элементы.Пароль.Видимость = Ложь;
				Элементы.ИзменитьПароль.Видимость = Истина;
			Иначе	
				Элементы.Пароль.Видимость = Истина;
				Элементы.ИзменитьПароль.Видимость = Ложь;
				ИзменениеПароля = Истина;
			КонецЕсли;	
				
		КонецЕсли;
		
	Иначе 
		
		Элементы.Логин.Доступность = Ложь;
		Элементы.Пароль.Доступность = Ложь;
		Элементы.ИзменитьПароль.Доступность = Ложь;
		Элементы.ПолныеПрава.Доступность = Ложь;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовый", ТекущийОбъект.ЭтоНовый());
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ТекущийОбъект.ИдентификаторПользователяИБ);
		
	Если ТекущийОбъект.Недействителен Тогда
		// Удаляем пользователя
		Если ПользовательИБ <> Неопределено Тогда
			ПользовательИБ.Удалить();
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	Если ПользовательИБ <> Неопределено Тогда
			
		// Обновление существующего пользователя
		ПользовательИБ.Имя = Логин;
		ПользовательИБ.ПолноеИмя = Логин;
		Если ИзменениеПароля Тогда
			ПользовательИБ.Пароль = Пароль; 
		КонецЕсли;	
		ПользовательИБ.Роли.Очистить();
		Если ПолныеПрава Тогда
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.АдминистраторСистемы);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.Пользователь);
		Иначе
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.Пользователь);
		КонецЕсли;
		
		ПользовательИБ.Записать();
		
	Иначе
		
		// Создание нового пользователя
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		ПользовательИБ.АутентификацияСтандартная = Истина;
		ПользовательИБ.Имя = Логин;
		ПользовательИБ.Пароль = Пароль;
		ПользовательИБ.ПолноеИмя = Логин; 
		ПользовательИБ.РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение;
		Если ПолныеПрава Тогда
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.АдминистраторСистемы);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.Пользователь);
		Иначе
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.Пользователь);
		КонецЕсли;
		
		ПользовательИБ.Записать();
		
		ТекущийОбъект.ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
		
		ОписаниеПользователяИБ = Новый Структура;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
		ОписаниеПользователяИБ.Вставить("УникальныйИдентификатор", ПользовательИБ.УникальныйИдентификатор);
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовый Тогда
		РаботаССообщениямиКлиент.ОбновитьИспользованиеСозданияСообщений();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПароль(Команда)
	
	Элементы.Пароль.Видимость = Истина;
	Элементы.ИзменитьПароль.Видимость = Ложь;
	ТекущийЭлемент = Элементы.Пароль;
	ИзменениеПароля = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Логин = ПользователиСлужебныйКлиентСервер.ПолучитьКраткоеИмяПользователяИБ(Объект.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура НедействителенПриИзменении(Элемент)
	
	Если Объект.Недействителен Тогда
		Логин = "";
		Элементы.Логин.Доступность = Ложь;
		Элементы.Пароль.Доступность = Ложь;
		Элементы.ИзменитьПароль.Доступность = Ложь;
		Элементы.ПолныеПрава.Доступность = Ложь;
	Иначе	
		Элементы.Логин.Доступность = Истина;
		Элементы.Пароль.Доступность = Истина;
		Элементы.ИзменитьПароль.Доступность = Истина;
		Элементы.ПолныеПрава.Доступность = Истина;
		Логин = ПользователиСлужебныйКлиентСервер.ПолучитьКраткоеИмяПользователяИБ(Объект.Наименование);
	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Объект.АдресЭлектроннойПочты)
		И Не РаботаСоСтроками.ЭтоАдресЭлектроннойПочты(Объект.АдресЭлектроннойПочты) Тогда
		
		ТекстОшибки = НСтр("ru = 'Укажите корректный адрес электронной почты'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,
				"Объект.АдресЭлектроннойПочты",, Отказ);
			
	КонецЕсли;
	
КонецПроцедуры

