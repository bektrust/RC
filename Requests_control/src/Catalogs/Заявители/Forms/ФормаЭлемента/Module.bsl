#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая()
		И Параметры.Свойство("ТекстЗаполнения")
		И ЗначениеЗаполнено(Параметры.ТекстЗаполнения) Тогда
		Объект.Наименование = Параметры.ТекстЗаполнения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.АдресЭлектроннойПочты)
		И Не ЗначениеЗаполнено(Объект.ПочтовыйАдрес) Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, НСтр("ru = 'Введите почтовый адрес или адрес электронной почты.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ПоказаноПредупреждениеОДубликатах
		И Объект.Ссылка.Пустая() Тогда
		Дубликаты = ПолучитьДубликаты();
		Если Дубликаты.Количество() > 0 Тогда
			Отказ = Истина;
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОДубликатах", ЭтаФорма);
			ПараметрыФормы = Новый Структура("Дубликаты", Дубликаты);
			ОткрытьФорму("Справочник.Заявители.Форма.ВопросОДубликатах",
				ПараметрыФормы,
				ЭтаФорма,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОДубликатах(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПоказаноПредупреждениеОДубликатах = Истина;
		Если Записать() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПоказаноПредупреждениеОДубликатах = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_Заявитель", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДубликаты()
	
	Дубликаты = Новый Массив;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заявители.Ссылка,
		|	Заявители.ПочтовыйАдрес,
		|	Заявители.АдресЭлектроннойПочты
		|ИЗ
		|	Справочник.Заявители КАК Заявители
		|ГДЕ
		|	НЕ Заявители.ПометкаУдаления
		|	И Заявители.Наименование ПОДОБНО &Наименование
		|	И (
		|		Заявители.ПочтовыйАдрес = """"
		|		ИЛИ Заявители.ПочтовыйАдрес ПОДОБНО &ПочтовыйАдрес
		|	)
		|	И (
		|		Заявители.АдресЭлектроннойПочты = """"
		|		ИЛИ Заявители.АдресЭлектроннойПочты ПОДОБНО &АдресЭлектроннойПочты
		|	)
		|");
	Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
	Запрос.УстановитьПараметр("ПочтовыйАдрес", Объект.ПочтовыйАдрес);
	Запрос.УстановитьПараметр("АдресЭлектроннойПочты", Объект.АдресЭлектроннойПочты);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Дубликат = Новый Структура;
		Дубликат.Вставить("Ссылка", Выборка.Ссылка);
		Дубликат.Вставить("ПочтовыйАдрес", Выборка.ПочтовыйАдрес);
		Дубликат.Вставить("АдресЭлектроннойПочты", Выборка.АдресЭлектроннойПочты);
		Дубликаты.Добавить(Дубликат);
	КонецЦикла;
	
	Возврат Дубликаты;
	
КонецФункции

#КонецОбласти