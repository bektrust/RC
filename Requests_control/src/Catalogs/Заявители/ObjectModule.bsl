
#Область Обработчики_событий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьОбращенияЗаявителя = Ложь;
	
	Если Не ЭтоНовый() Тогда
		СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Наименование");
		ОбновитьОбращенияЗаявителя = Не СтрСравнить(СтарыеРеквизиты.Наименование, Наименование) = 0;		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ОбновитьОбращенияЗаявителя", ОбновитьОбращенияЗаявителя);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновитьОбращенияЗаявителя") И ДополнительныеСвойства.ОбновитьОбращенияЗаявителя Тогда
		Справочники.Обращения.ОбновитьОбращенияПриИзмененииЗаявителя(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти