#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДанныеВыбора = Новый СписокЗначений;
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 20
		|	Заявители.Ссылка,
		|	Заявители.Наименование,
		|	Заявители.ПочтовыйАдрес,
		|	Заявители.АдресЭлектроннойПочты
		|ИЗ
		|	Справочник.Заявители КАК Заявители
		|ГДЕ
		|	НЕ Заявители.ПометкаУдаления
		|	И (Заявители.Наименование ПОДОБНО &СтрокаПоиска
		|		ИЛИ Заявители.АдресЭлектроннойПочты ПОДОБНО &СтрокаПоиска)
		|");
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеФорматированнаяСтрока = Новый ФорматированнаяСтрока(Выборка.Наименование);
		
		Если ЗначениеЗаполнено(Выборка.АдресЭлектроннойПочты) Тогда
			
			ПредставлениеФорматированнаяСтрока = Новый ФорматированнаяСтрока(
				ПредставлениеФорматированнаяСтрока, 
				Новый ФорматированнаяСтрока(" " + Выборка.АдресЭлектроннойПочты, , WebЦвета.Серый));
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ПочтовыйАдрес) Тогда
			
			ПредставлениеФорматированнаяСтрока = Новый ФорматированнаяСтрока(
				ПредставлениеФорматированнаяСтрока, 
				Новый ФорматированнаяСтрока(" " + Выборка.ПочтовыйАдрес, , WebЦвета.Серый));
				
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Выборка.Ссылка, ПредставлениеФорматированнаяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти