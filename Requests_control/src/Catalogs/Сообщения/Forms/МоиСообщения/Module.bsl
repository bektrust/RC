#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра(
		"ТекущийПользователь", 
		Пользователи.ТекущийПользователь());
	
	ПоказыватьУдаленные = Ложь;
	ПоказатьУдаленные();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСозданиеСообщений") Тогда
		
		Если Элементы.Найти("ФормаСоздать") <> Неопределено Тогда
			Элементы.ФормаСоздать.Видимость = Ложь;
		КонецЕсли;
		
		Если Элементы.Найти("ФормаСкопировать") <> Неопределено Тогда
			Элементы.ФормаСкопировать.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.Предмет Тогда
		
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
		Если ДанныеСтроки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеСтроки.Предмет) Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение( , ДанныеСтроки.Предмет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ПараметрыВыделенияСсылок = РаботаС_HTML.ПараметрыВыделенияСсылок();
	ПараметрыВыделенияСсылок.ПолучатьПредставление = Истина;
	ПараметрыВыделенияСсылок.ФормироватьТеги = Ложь;
	
	Для Каждого Строка Из Строки Цикл
		
		Данные = Строка.Значение.Данные;
		Если Данные.Свойство("Текст") Тогда
			РаботаС_HTML.ДобавитьТегиКСсылкам(Данные.Текст, ПараметрыВыделенияСсылок);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		
		Элементы.ФормаПоказыватьУдаленные.Пометка = Истина;
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			Список, "ПометкаУдаления");		
			
	Иначе
		
		Элементы.ФормаПоказыватьУдаленные.Пометка = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ПометкаУдаления", Ложь);
			
	КонецЕсли;
		
КонецПроцедуры		

#КонецОбласти